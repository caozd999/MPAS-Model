! Copyright (c) 2013,  Los Alamos National Security, LLC (LANS)
! and the University Corporation for Atmospheric Research (UCAR).
!
! Unless noted otherwise source code is licensed under the BSD license.
! Additional copyright and license information can be found in the LICENSE file
! distributed with this code, or at http://mpas-dev.github.com/license.html
!
!|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
!
!  ocn_vel_forcing_explicit_vegetation_drag
!
!> \brief MPAS ocean explicit vegetation drag
!> \author Zhendong Cao
!> \date   April 2020
!> \details
!>  This module contains the routine for computing
!>  tendencies from explicit vegetation drag.
!
!-----------------------------------------------------------------------

module ocn_vel_forcing_explicit_vegetation_drag

   use mpas_derived_types
   use mpas_pool_routines
   use mpas_timer
   use mpas_constants, only : gravity
   use ocn_constants
   use ocn_forcing

   implicit none
   private
   save

   !--------------------------------------------------------------------
   !
   ! Public parameters
   !
   !--------------------------------------------------------------------

   !--------------------------------------------------------------------
   !
   ! Public member functions
   !
   !--------------------------------------------------------------------

   public :: ocn_vel_forcing_explicit_vegetation_drag_tend, &
             ocn_vel_forcing_explicit_vegetation_drag_init

   !--------------------------------------------------------------------
   !
   ! Private module variables
   !
   !--------------------------------------------------------------------
   logical :: explicitVegetationDragOn
   
!***********************************************************************

contains

!***********************************************************************
!
!  routine ocn_vel_forcing_explicit_vegetation_drag_tend
!
!> \brief   Computes tendency term from explicit vegetation drag
!> \author  Zhendong Cao
!> \date    23 April 2020
!> \details
!>  This routine computes the explicit vegetation drag tendency for momentum
!>  based on current state.
!
!-----------------------------------------------------------------------

   subroutine ocn_vel_forcing_explicit_vegetation_drag_tend(meshPool, ssh, bottomDrag, & !{{{
                            normalVelocity, kineticEnergyCell, layerThicknessEdge, tend, err)

      !-----------------------------------------------------------------
      !
      ! input variables
      !
      !-----------------------------------------------------------------

      real (kind=RKIND), dimension(:,:), intent(in) :: &
         normalVelocity,       &!< Input: velocity
         kineticEnergyCell,    &!< Input: kinetic energy at cell
         layerThicknessEdge     !< Input: thickness at edge
      real (kind=RKIND), dimension(:), intent(in) ::  &
        ssh                     !< Input: sea surface height
      type (mpas_pool_type), intent(in) :: &
         meshPool             !< Input: mesh information
      !-----------------------------------------------------------------
      !
      ! input/output variables
      !
      !-----------------------------------------------------------------

      real (kind=RKIND), dimension(:,:), intent(inout) :: &
         tend          !< Input/Output: velocity tendency
      real (kind=RKIND), dimension(:), intent(in) :: &
         bottomDrag              !< Input: bottom drag coefficient
      !-----------------------------------------------------------------
      !
      ! output variables
      !
      !-----------------------------------------------------------------

      integer, intent(out) :: err !< Output: error flag

      !-----------------------------------------------------------------
      !
      ! local variables
      !
      !-----------------------------------------------------------------

      logical, pointer :: config_use_explicit_vegetation_drag
      integer :: N, iEdge, k, cell1, cell2, nEdges
      integer, dimension(:), pointer :: nEdgesArray
      integer, dimension(:), pointer :: maxLevelEdgeTop
      integer, dimension(:,:), pointer :: cellsOnEdge
      integer, pointer :: nVertLevels
      integer, dimension(:), pointer :: xCell, yCell
      real(kind=RKIND), dimension (:), pointer :: bottomDepth

      real(kind=RKIND), pointer :: config_idealized_profile_Cd, &
                                   config_idealized_profile_Cd_marsh, &
                                   config_vegetation_manningN, &
                                   config_vegetation_biomass, &
                                   config_vegetation_density, &
                                   config_vegetation_diameter, &
                                   config_vegetation_height
      real(kind=RKIND) :: Cd_veg, implicitCd, cff, mask_veg
      real (kind=RKIND), dimension(:), allocatable :: R_veg, dab


      err = 0
      if ( .not. explicitVegetationDragOn ) return

      call mpas_timer_start('vel explicit vegetation drag')

      call mpas_pool_get_dimension(meshPool, 'nEdgesArray', nEdgesArray)
      call mpas_pool_get_dimension(meshPool, 'nVertLevels', nVertLevels)
      call mpas_pool_get_array(meshPool, 'cellsOnEdge', cellsOnEdge)
      call mpas_pool_get_array(meshPool, 'maxLevelEdgeTop', maxLevelEdgeTop)
      call mpas_pool_get_array(meshPool, 'bottomDepth', bottomDepth)
      call mpas_pool_get_array(meshPool, 'yCell', xCell)
      call mpas_pool_get_array(meshPool, 'yCell', yCell)

! read in vegetation properties
      call mpas_pool_get_config(ocnConfigs, 'config_idealized_profile_Cd',config_idealized_profile_Cd)
      call mpas_pool_get_config(ocnConfigs, 'config_idealized_profile_Cd_marsh',config_idealized_profile_Cd_marsh)
      call mpas_pool_get_config(ocnConfigs, 'config_vegetation_manningN',config_vegetation_manningN)
      call mpas_pool_get_config(ocnConfigs, 'config_vegetation_biomass',config_vegetation_biomass)
      call mpas_pool_get_config(ocnConfigs, 'config_vegetation_diameter',config_vegetation_diameter)
      call mpas_pool_get_config(ocnConfigs, 'config_vegetation_height',config_vegetation_height)
      call mpas_pool_get_config(ocnConfigs, 'config_vegetation_density',config_vegetation_density)

      allocate(R_veg(nVertLevels), dab(nVertLevels+1))

      nEdges = nEdgesArray( 1 )

      !$omp do schedule(runtime) private(k, cell1, cell2)
      do iEdge = 1, nEdges
        N =  maxLevelEdgeTop(iEdge)
        if (N .gt. 0) then
          cell1 = cellsOnEdge(1,iEdge)
          cell2 = cellsOnEdge(2,iEdge)

          mask_veg = 0.0_RKIND
          if(bottomDrag(cell1).ne.config_idealized_profile_Cd .OR. &
                                  bottomDrag(cell2).ne.config_idealized_profile_Cd) then
            mask_veg = 1.0_RKIND
          endif

          ! average cell-based implicit bottom drag to edges and convert Mannings n to Cd
!          implicitCd = gravity * config_vegetation_manningN**2.0_RKIND * &
!                       (0.5_RKIND * (ssh(cell1) + ssh(cell2) + bottomDepth(cell1) + bottomDepth(cell2)))**(-1.0_RKIND/3.0_RKIND)

          implicitCd = 0.5_RKIND * (bottomDrag(cell1) + bottomDrag(cell2))

          ! compute the relative height of the vegetation at vertical layer, R_veg
          ! code adopted from COAWST
          dab = 0.0_RKIND
          do k = N,1,-1
            dab(k) = dab(k+1) + layerThicknessEdge(k,iEdge)
            cff = MIN(1.0_RKIND, (dab(k)-config_vegetation_height)/layerThicknessEdge(k,iEdge))
            R_veg(k) = MIN(1-cff, 1.0_RKIND)
          enddo

          ! compute the vegetation drag
          ! Explicit vegetation drag term:
          ! du/dt = ... - c |u| u / h
          ! appied to all vegetation layer only.
          ! This term comes from the vegetation boundary condition in the vertical
          ! momentum mixing, and is explicit if both |u| and u are chosen to be at
          ! time level n. 
          do k = 1, N-1
            Cd_veg = implicitCd*layerThicknessEdge(k,iEdge)*R_veg(k)*mask_veg
            tend(k,iEdge) = tend(k,iEdge) - Cd_veg* &
                           sqrt(kineticEnergyCell(k,cell1) + kineticEnergyCell(k,cell2)) * normalVelocity(k,iEdge)  &
                            / layerThicknessEdge(k,iEdge)
          enddo

        endif
      enddo
      !$omp end do

      deallocate(R_veg, dab)

      call mpas_timer_stop('vel explicit vegetation drag')

   !--------------------------------------------------------------------

   end subroutine ocn_vel_forcing_explicit_vegetation_drag_tend!}}}

!***********************************************************************
!
!  routine ocn_vel_forcing_explicit_vegetation_drag_init
!
!> \brief   Initializes ocean explicit vegetation drag forcing
!> \author  Zhendong Cao
!> \date    April 2020
!> \details
!>  This routine initializes quantities related to explicit vegetation drag
!>  in the ocean.
!
!-----------------------------------------------------------------------

   subroutine ocn_vel_forcing_explicit_vegetation_drag_init(err)!{{{

   !--------------------------------------------------------------------

      !-----------------------------------------------------------------
      !
      ! call individual init routines for each parameterization
      !
      !-----------------------------------------------------------------

      integer, intent(out) :: err !< Output: error flag

      logical, pointer :: config_use_explicit_vegetation_drag
      logical, pointer :: config_idealized_profile
      integer, pointer :: nVertLevels
      integer, dimension(:), pointer :: nEdgesArray
      integer, dimension(:), pointer :: maxLevelEdgeTop
      integer, dimension(:,:), pointer :: cellsOnEdge

      real (kind=RKIND) :: implicitCd
      integer :: iEdge, k, cell1, cell2, N, nEdges

      err = 0

      call mpas_pool_get_config(ocnConfigs, 'config_idealized_profile', config_idealized_profile)
      call mpas_pool_get_config(ocnConfigs, 'config_use_explicit_vegetation_drag', config_use_explicit_vegetation_drag)
      if (config_idealized_profile .and. config_use_explicit_vegetation_drag ) then
        explicitVegetationDragOn = .TRUE.
        call mpas_log_write('explicitVegetationDragOn = .TRUE. ')
      endif
   !--------------------------------------------------------------------

   end subroutine ocn_vel_forcing_explicit_vegetation_drag_init!}}}

!***********************************************************************

end module ocn_vel_forcing_explicit_vegetation_drag

!|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
! vim: foldmethod=marker
